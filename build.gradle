buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'https://oss.sonatype.org/content/groups/public/'
        }
        maven {
            name 'forge'
            url 'http://files.minecraftforge.net/maven'
        }
    }

    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.0-SNAPSHOT'
        classpath 'org.ajoberstar:gradle-git:0.7.0-SNAPSHOT'
    }
}

apply plugin: 'forge'
apply plugin: 'grgit'
apply plugin: 'maven'

configurations {
    deployerJars
}
repositories { mavenCentral() }
dependencies {
    deployerJars 'org.springframework.build.aws:org.springframework.build.aws.maven:3.0.0.RELEASE'
}


group = 'net.cazzar.corelib'
version = '1.0.2'
def actualVersion = version
archivesBaseName = 'CazzarCoreLib'

minecraft { version = '1.6.4-9.11.1.964' }
logger.lifecycle "$archivesBaseName-$version"


if (new File(projectDir, '.git').exists()) {
    def repo = grgit.repo('.')
    version = "MC${minecraft.version}-$version-${repo.history.log().find().abbreviatedId}"
    actualVersion += "-${repo.history.log().find().abbreviatedId}"
} else {
    version = "MC${minecraft.version}-$version-DEV"
    actualVersion += "-DEV"
}

processResources {
    // replace stuff in text files, not binary ones.
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.lang'
        include '**/*.info'

        expand 'version': project.version, 'mcversion': project.minecraft.version
        // replace version and MCVersion
    }

    // copy everything else, that's not text
    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.lang'
        exclude '**/*.info'
    }
}

//REGION: artifacts.
jar {
    classifier = 'universal'
    manifest {
        attributes FMLCorePlugin: "net.cazzar.corelib.CoreMod",
                "Implementation-Version": actualVersion
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'src'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    appendix = 'javadoc-' + project.minecraft.version
    from 'build/docs/javadoc'
}

// because the normal default jar task has been modified to be obfuscated
task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'

    manifest {
        attributes FMLCorePlugin: "net.cazzar.corelib.CoreMod",
                "Implementation-Version": actualVersion
    }
}

artifacts { archives deobfJar }
//ENDREGION: artifacts.
//REGION: Wrapper info
wrapper {
    version = "1.8"
}
//ENDREGION: Wrapper info

uploadArchives {
    repositories.mavenDeployer {
        configuration = configurations.deployerJars

        repository(url: 's3://CazzarMods/repo/') {
            authentication([userName: project.properties.awsUserID, passphrase: project.properties.awsSecretKey])
        }
        pom {
            groupId = project.group
            version = project.version
            artifactId = project.archivesBaseName

            project {
                name project.archivesBaseName
                packaging 'jar'
                description 'Cazzar\'s Core Library for mods'
                url ''

                scm {
                    url 'https://bitbucket.org/cazzar/cazzarcorelib'
                    connection 'scm:git:https://bitbucket.org/cazzar/cazzarcorelib.git'
                    developerConnection 'scm:git:git@bitbucket.org:cazzar/cazzarcorelib.git'
                }
                issueManagement {
                    system 'bitbucket'
                    url 'https://bitbucket.org/cazzar/cazzarcorelib/issues'
                }

                licenses {
                    license {
                        name 'GNU GENERAL PUBLIC LICENSE Version 3'
                        url 'https://www.gnu.org/licenses/gpl-3.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'cazzar'
                        name 'Cayde Dixon'
                        roles {
                            role 'packager'
                            role 'developer'
                        }
                    }
                }
            }
        }
    }
}