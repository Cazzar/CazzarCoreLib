buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'https://oss.sonatype.org/content/groups/public/'
        }
        maven {
            name 'forge'
            url 'http://files.minecraftforge.net/maven'
        }
        maven {
            name 'Cazzar\'s Maven repo'
            url 'http://maven.cazzar.net/'
        }
    }

    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.1-SNAPSHOT'
        classpath 'org.ajoberstar:gradle-git:0.7.0-SNAPSHOT'
        classpath 'net.cazzar:CurseForgeUploader:1.0'
    }
}

import org.ajoberstar.gradle.git.tasks.*


apply plugin: 'forge'
apply plugin: 'grgit'
apply plugin: 'maven-publish'
apply plugin: 'curseforge'

configurations {
    deployerJars
}
repositories { mavenCentral() }
dependencies {
    deployerJars 'org.springframework.build.aws:org.springframework.build.aws.maven:3.0.0.RELEASE'
//    compile "org.ajoberstar:gradle-git:0.7.0-SNAPSHOT"
}


group = 'net.cazzar.corelib'
version = '1.1.0'
def actualVersion = version
archivesBaseName = 'CazzarCoreLib'

minecraft {
    version = '1.7.2-10.12.0.1019'
    assetDir = '../run/assets'
}

//logger.lifecycle "$archivesBaseName-$version"

if (System.getenv("BUILD_NUMBER") != null) {
    version = "MC${minecraft.version}-$version." + System.getenv("BUILD_NUMBER");
    actualVersion += "." + System.getenv("BUILD_NUMBER");
} else if (new File(projectDir, '.git').exists()) {
    def repo = grgit.repo('.')
    version = "MC${minecraft.version}-$version-${repo.history.log().find().abbreviatedId}"
    actualVersion += "-${repo.history.log().find().abbreviatedId}"
} else {
    version = "MC${minecraft.version}-$version-DEV"
    actualVersion += "-DEV"
}



processResources {
    // replace stuff in text files, not binary ones.
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.lang'
        include '**/*.info'

        expand 'version': project.version, 'mcversion': project.minecraft.version
        // replace version and MCVersion
    }

    // copy everything else, that's not text
    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.lang'
        exclude '**/*.info'
    }
}

//REGION: artifacts.
jar {
    classifier = 'universal'
    manifest {
        attributes FMLCorePlugin: "net.cazzar.corelib.CoreMod",
                "Implementation-Version": actualVersion
    }
    from 'build/unpacked/conf/mcp-srg.srg' into '/'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'src'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
//    appendix = 'javadoc-' + project.minecraft.version
    classifier 'javadoc'
    from 'build/docs/javadoc'
}

// because the normal default jar task has been modified to be obfuscated
task deobfJar(type: Jar) {
    from sourceSets.main.output
    //Since this is now the main version.
    classifier = ''

    manifest {
        attributes FMLCorePlugin: "net.cazzar.corelib.CoreMod",
                "Implementation-Version": actualVersion
    }
}

artifacts {
    archives deobfJar
    archives javadocJar
}
//ENDREGION: artifacts.

task tag(type: GitTag) {
    tagName = actualVersion
    message = "Release of ${version}"
}

task push(type: GitPush) {
    tags = true;
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact javadocJar
            artifact deobfJar
        }
    }
    repositories {
        maven {
            url "file://home/cayde/www-data/cazzar.net/maven"
        }
    }
}

import groovy.json.JsonSlurper
def getJenkinsChanges() {
	def jsonSlurper = new JsonSlurper();
    def url = new URL('http://direct.cazzar.net:8080/job/CazzarCore-1.7/lastBuild/api/json')
    def connection = url.openConnection();

    def reader = new BufferedReader(new InputStreamReader(connection.getInputStream()))

    def result = jsonSlurper.parse(reader)

//    println result.changeSet.items


    def data = new ArrayList<Object>()

    for (obj in result.changeSet.items) {
        data.add(obj.author.fullName + ": " + obj.msg)
    }

    def iter = data.iterator()
    StringBuilder val = new StringBuilder()
    while (iter.hasNext()) {
        val.append(iter.next()).append((iter.hasNext()) ? '\n' : '')
    }

    return val.toString()
}

uploadToCurseForge {
    api_key project.curseAPI
    stub 'cazzarcorelib'
    artifact_name 'Version ' + actualVersion
    game_version minecraft.version
    artifact tasks.reobf.outputs.getFiles().getSingleFile()
    change_log getJenkinsChanges()
}